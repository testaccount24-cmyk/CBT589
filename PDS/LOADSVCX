LOADSVCX CSECT
         BAKR  R14,0               LINKAGE STACK
         LR    R12,R15             CARREGA ENTRY POINT EM R12
         USING LOADSVCX,R12        ESTABELECE ENDERECAMENTO
         MODID ,                   EYE-CATCHER E DATA
**********************************************************************
* PROGRAM: LOADSVCX                                                  *
*                                                                    *
*         THIS PROGRAM WAS ORIGINALLY WRITTEN BY PROFESSOR           *
*   CARMINE CANATELLO AND I'VE MADE A LOT OF ENHANCEMENTS.           *
*                                                                    *
*                                                                    *
* FUNCTION: BATCH PROGRAM TO DYNAMICALLY LOAD AN SVC.                *
*                                                                    *
* PARAMETERS: O,NNN,MMMMMMMM                                         *
*                                                                    *
*    WHERE:                                                          *
*                                                                    *
*         O - EXTENDED SVC ?    -  Y(YES) OR N(NO)                   *
*                                                                    *
*         NNN - SVC NUMBER                                           *
*                                                                    *
*         MMMMMMMM - MODULE NAME TO BE LOADED.                       *
*                                                                    *
*                                                                    *
* EXAMPLE:                                                           *
*                                                                    *
*   THE FOLLOWING JOB LOADS AN EXTENDED SVC 235 (ESR 109)            *
*                                                                    *
*                                                                    *
*  //JOBNAME JOB (P,SI,XXXXXX),CLASS=A,MSGCLASS=X,MSGLEVEL=(1,1)     *
*  //*                                                               *
*  //ST001    EXEC PGM=LOADSVCX,PARM='Y,235,MYSVC'                   *
*  //SVCLIB   DD DSN=SYS2.LINKLIB,DISP=SHR                           *
*                                                                    *
*                                                                    *
**********************************************************************
*----------------------------------------------------------------------
* CORPO PRINCIPAL DO PROGRAMA
*----------------------------------------------------------------------
         BAS   R6,GETPARM
         BAS   R6,VERSVCNO
         BAS   R6,CNVSVCNO
         BAS   R6,SETMODE
         BAS   R6,LOADNSVC
         BAS   R6,SETNWSVC
         BAS   R6,SETNXSVC
         B     RETURN
*----------------------------------------------------------------------
* CAPTURA PARM
*----------------------------------------------------------------------
GETPARM  L     R1,0(0,R1)
         LH    R15,0(0,R1)
         LTR   R15,R15
         BZ    NOPARM
         BCTR  R15,0
         EX    R15,MOVE
         BR    R6
MOVE     MVC   SVCINFO(0),2(R1)
NOPARM   WTO   '*** SVC NUMBER AND NAME NOT SPECIFIED---PROGRAM TERMINA-
               TED ***',ROUTCDE=11
         ABEND 901
*----------------------------------------------------------------------
* VERIFICA SE A SVC EH VALIDA
*----------------------------------------------------------------------
VERSVCNO LA    R0,3
         LA    R10,DECSVCNO
NXTSVCDG CLI   0(R10),C'0'
         BL    INVSVCNO
         CLI   0(R10),C'9'
         BH    INVSVCNO
         LA    R10,1(0,R10)
         BCT   R0,NXTSVCDG
         CLC   DECSVCNO,=C'200'
         BL    INVSVCNO
         CLC   DECSVCNO,=C'255'
         BH    INVSVCNO
         BR    R6
INVSVCNO WTO   '*** INVALID SVC NUMBER SPECIFIED---PROGRAM TERMINATED *-
               **',ROUTCDE=11
         ABEND 902
*----------------------------------------------------------------------
* CONVERTE O NUMERO DA SVC DE EBCDIC PARA HEXADECIMAL
*----------------------------------------------------------------------
CNVSVCNO PACK  PKSVCNO,DECSVCNO
         CVB   R10,PKSVCNO
         ST    R10,SVCNUM
         BR    R6
*----------------------------------------------------------------------
* FAZ O SWITCH PARA MODO SUPERVISOR E PSWKEY ZERO
*----------------------------------------------------------------------
SETMODE  MODESET MODE=SUP,KEY=ZERO
         BR    R6
*----------------------------------------------------------------------
* CARREGA MODULO PASSADO VIA PARAMETRO EM CSA
*----------------------------------------------------------------------
LOADNSVC BAS   R7,OPENLIB
         MVC   LPMEANAME(8),SVCNAME
         LA    R2,LPMEA
         ENQ   (QNAME1,RNAME1,E,,SYSTEM)
         CSVDYLPA REQUEST=ADD,                                         X
               MODINFOTYPE=MEMBERLIST,                                 X
               DCB=SVCLIB,                                             X
               APFREQUIRED=NO,                                         X
               REQUESTOR=LREQ,                                         X
               MODINFO=(2),                                            X
               NUMMOD=LMODN
         DEQ   (QNAME1,RNAME1,,SYSTEM)
         ST    R0,SVCEPA
         BAS   R7,CLOSELIB
         BR    R6
*----------------------------------------------------------------------
* OPEN NA SVCLIB
*----------------------------------------------------------------------
OPENLIB  OPEN  SVCLIB
         BR    R7
*----------------------------------------------------------------------
* CLOSE NA SVCLIB
*----------------------------------------------------------------------
CLOSELIB CLOSE SVCLIB
         BR    R7
*----------------------------------------------------------------------
* FAZ UPDATE NA SVCTABLE (NORMAL)
*----------------------------------------------------------------------
SETNWSVC EQU   *
         CLI   OPTION,C'N'
         BNE   OUTNSVC
         L     R10,LPMEAENTRYPOINTADDR
         L     R11,SVCNUM
         ENQ   (QNAME2,RNAME2,E,,SYSTEM)
         SVCUPDTE (R11),REPLACE,TYPE=3,EP=(R10)
         DEQ   (QNAME2,RNAME2,,SYSTEM)
         LTR   R15,R15
         BZR   R6
         WTO   '*** ERROR UPDATING SVC TABLE---PROGRAM TERMINATED ***'
         ABEND 903,DUMP
OUTNSVC  EQU   *
         BR    R6
*----------------------------------------------------------------------
* FAZ UPDATE NA SVCTABLE (EXTENDIDA)
*----------------------------------------------------------------------
SETNXSVC EQU   *
         CLI   OPTION,C'Y'
         BNE   OUTXSVC
         AIF   (('&SYSTEM_ID'(1,6) EQ 'OS/390') AND                    X
               ('&SYSTEM_ID'(11,2) LT '10')).NOTSUP
         L     R10,LPMEAENTRYPOINTADDR
         L     R11,SVCNUM
         ENQ   (QNAME2,RNAME2,E,,SYSTEM)
         SVCUPDTE 109,ESR=(R11),REPLACE,TYPE=3,EP=(R10)
         DEQ   (QNAME2,RNAME2,,SYSTEM)
         LTR   R15,R15
         BZR   R6
         WTO   '*** ERROR UPDATING SVC TABLE---PROGRAM TERMINATED ***'
         ABEND 904,DUMP
.NOTSUP  ANOP
         WTO   '*** UPDATE EXTENDED SVC NOT SUPPORTED ***'
         ABEND 905,DUMP
OUTXSVC  EQU   *
         BR    R6
*----------------------------------------------------------------------
* RETORNA AO SISTEMA OPERATIVO
*----------------------------------------------------------------------
RETURN   EQU   *
         SLR   R15,R15
         PR
*----------------------------------------------------------------------
* AREA DE VARIAVEIS E CONSTANTES
*----------------------------------------------------------------------
PKSVCNO  DS    D
LMODN    DC    F'1'
LREQ     DC    CL16'LOADSVCX'
SVCNUM   DS    F
SVCEPA   DS    F
SVCINFO  DS    0CL14
OPTION   DS    CL1
         DS    CL1
DECSVCNO DS    CL3
         DS    CL1
SVCNAME  DC    CL8' '
QNAME1   DC    CL8'SYSZCSV'
RNAME1   DC    C'CSVDYLPA'
QNAME2   DC    CL8'SYSZSVC'
RNAME2   DC    C'TABLE'
         CSVLPRET DSECT=NO
*----------------------------------------------------------------------
* DCB SVCLIB
*----------------------------------------------------------------------
SVCLIB   DCB   DSORG=PO,MACRF=R,DDNAME=SVCLIB
*----------------------------------------------------------------------
* DUMMY SECTIONS E EQUATES
*----------------------------------------------------------------------
         CVT   DSECT=YES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
