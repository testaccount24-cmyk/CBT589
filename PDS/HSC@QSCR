HSC@QSCR CSECT
HSC@QSCR AMODE 31
HSC@QSCR RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING HSC@QSCR,R12
***********************************************************************
* QSCRATCH FUNCTION IMPLEMENTATION                                    *
***********************************************************************
         LR    R11,R1
         USING WORKAREA,R11
*
         SLSXREQ QSCRATCH,                                             X
               OPTION=SYNCH,                                           X
               XCALADR=WXCALADR,                                       X
               MF=(E,SLSXPARM)
         LTR   R15,R15
         BNZ   HSCNOACT
         LR    R7,R1
         USING SLX,R7
         XR    R15,R15
         ICM   R15,B'0001',SLXCMDRC
         LA    R3,24
         CR    R15,R3
         BH    ERROINVL
         B     *+4(R15)
         B     CMDOK                   RC = 00 ?
         B     ERROQSCR                RC = 04 ?
         B     ERROPARM                RC = 08 ?
         B     ERROUNRC                RC = 12 ?
         B     ERROUNSC                RC = 16 ?
         B     HSCNOACT                RC = 20 ?
CMDOK    EQU   *
         L     R5,SLXXLSMN
         L     R4,SLXXLSML
         L     R2,SLXXLSMO
         LA    R2,SLX(2)
         USING SLXSLSM,R2
         BAS   R6,SETVAR
         PR
ERROQSCR EQU   *
         LA    R15,4
         PR
ERROPARM EQU   *                       INVALID PARM SPECIFIED
         LA    R15,8
         PR
ERROUNRC EQU   *                       UNRECOVERABLE ERROR
         LA    R15,12
         PR
ERROUNSC EQU   *                       UNSUCCESSFUL REQUEST
         BAS   R6,SETVARER
         LA    R15,16
         PR
HSCNOACT EQU   *                       HSC NOT AVAILABLE
         LA    R15,20
         PR
ERRONAUT EQU   *                       FUNCTION NOT AUTHORIZED
         LA    R15,24
         PR
ERROINVL EQU   *                       INVALID RETURN CODE
         LA    R15,50
         PR
ERROSETV EQU   *                       SETVAR ROUTINE ERROR
         LA    R15,60
         PR
***********************************************************************
* SET VARIABLES SUBROUTINE                                            *
***********************************************************************
SETVAR   EQU   *
         ZAP   STEMCONT(2),=P'1'
*
* SET VARIABLE @SLXMACS.0 => NUMBER OF ENTRIES
*
         MVC   NAMEVAR(10),=C'@SLXMACS.0'
         MVC   SHVNAML(4),=F'10'
         CVD   R5,DECNUMBR
         UNPK  BCDNUMBR(2),DECNUMBR(8)
         OC    BCDNUMBR(2),=X'F0F0'
         MVC   VALUEVAR(2),BCDNUMBR
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV
NEXTSTEM EQU   *
*
* SET STEM @SLXMACS  => ACS ID
*
         MVC   NAMEVAR(11),=C'@SLXMACS.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTMAC1
         MVC   SHVNAML(4),=F'10'
         UNPK  NAMEVAR+9(1),STEMCONT(2)
         OI    NAMEVAR+9,X'F0'
         B     CONTMAC2
CONTMAC1 EQU   *
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+9(2),STEMCONT(2)
         OC    NAMEVAR+9,=X'F0F0'
CONTMAC2 EQU   *
         MVC   HEXNUMBR(1),SLXMACS
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=2X'0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV
*
* SET STEM @SLXMLSM  => LSM ID
*
         MVC   NAMEVAR(11),=C'@SLXMLSM.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTMLS1
         MVC   SHVNAML(4),=F'10'
         UNPK  NAMEVAR+9(1),STEMCONT(2)
         OI    NAMEVAR+9,X'F0'
         B     CONTMLS2
CONTMLS1 EQU   *
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+9(2),STEMCONT(2)
         OC    NAMEVAR+9,=X'F0F0'
CONTMLS2 EQU   *
         MVC   HEXNUMBR(1),SLXMLSM
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=2X'0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV
*
* SET STEM @SLXMADJN => NUMBER OF ADJACENTS LSM'S
*
         MVC   NAMEVAR(12),=C'@SLXMADJN.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTMAD1
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         B     CONTMAD2
CONTMAD1 EQU   *
         MVC   SHVNAML(4),=F'12'
         UNPK  NAMEVAR+10(2),STEMCONT(2)
         OC    NAMEVAR+10,=X'F0F0'
CONTMAD2 EQU   *
         SLR   R9,R9
         ICM   R9,B'0001',SLXMADJN
         CVD   R9,DECNUMBR
         UNPK  BCDNUMBR(2),DECNUMBR(8)
         OC    BCDNUMBR(2),=X'F0F0'
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV
*
* SET STEM @SLXMNSCR => SCRATCH COUNT FOR LSM
*
         MVC   NAMEVAR(12),=C'@SLXMNSCR.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTMNS1
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         B     CONTMNS2
CONTMNS1 EQU   *
         MVC   SHVNAML(4),=F'12'
         UNPK  NAMEVAR+10(2),STEMCONT(2)
         OC    NAMEVAR+10,=X'F0F0'
CONTMNS2 EQU   *
         SLR   R9,R9
         L     R9,SLXMNSCR
         CVD   R9,DECNUMBR
         UNPK  BCDNUMBR(5),DECNUMBR(8)
         OC    BCDNUMBR(5),=5X'F0'
         MVC   VALUEVAR(5),BCDNUMBR
         MVC   SHVVALL(4),=F'5'
         BAS   R8,HSCSETV
*
* SET STEM @SLXMTCEL => TOTAL CELLS IN LSM
*
         MVC   NAMEVAR(12),=C'@SLXMTCEL.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTMTC1
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         B     CONTMTC2
CONTMTC1 EQU   *
         MVC   SHVNAML(4),=F'12'
         UNPK  NAMEVAR+10(2),STEMCONT(2)
         OC    NAMEVAR+10,=X'F0F0'
CONTMTC2 EQU   *
         SLR   R9,R9
         L     R9,SLXMTCEL
         CVD   R9,DECNUMBR
         UNPK  BCDNUMBR(5),DECNUMBR(8)
         OC    BCDNUMBR(5),=5X'F0'
         MVC   VALUEVAR(5),BCDNUMBR
         MVC   SHVVALL(4),=F'5'
         BAS   R8,HSCSETV
*
* SET STEM @SLXMFCEL => FREE CELLS IN LSM
*
         MVC   NAMEVAR(12),=C'@SLXMFCEL.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTMFC1
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         B     CONTMFC2
CONTMFC1 EQU   *
         MVC   SHVNAML(4),=F'12'
         UNPK  NAMEVAR+10(2),STEMCONT(2)
         OC    NAMEVAR+10,=X'F0F0'
CONTMFC2 EQU   *
         SLR   R9,R9
         L     R9,SLXMFCEL
         CVD   R9,DECNUMBR
         UNPK  BCDNUMBR(5),DECNUMBR(8)
         OC    BCDNUMBR(5),=5X'F0'
         MVC   VALUEVAR(5),BCDNUMBR
         MVC   SHVVALL(4),=F'5'
         BAS   R8,HSCSETV
*
         LA    R2,SLXSLSML(,R2)        NEXT DSN ELEMENT
         AP    STEMCONT(2),=P'1'
         BCT   R5,NEXTSTEM
*
* SET VARIABLE @SLXCMDRC => RETURN CODE
*
SETVARER EQU   *
         MVC   NAMEVAR(9),=C'@SLXCMDRC'
         MVC   SHVNAML(4),=F'9'
         MVC   HEXNUMBR(1),SLXCMDRC
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=X'0F0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV
*
* SET VARIABLE @SLXSRC => REASON CODE
*
         MVC   NAMEVAR(7),=C'@SLXSRC'
         MVC   SHVNAML(4),=F'7'
         MVC   HEXNUMBR(4),SLXSRC
         UNPK  BCDNUMBR(9),HEXNUMBR(5)
         NC    BCDNUMBR,=X'0F0F0F0F0F0F0F0F'
         TR    BCDNUMBR(8),HEXTABLE
         MVC   VALUEVAR(8),BCDNUMBR
         MVC   SHVVALL(4),=F'8'
         BAS   R8,HSCSETV
*
         BR    R6
*
*     CALL HSC@SETV (SET VARIABLE ROUTINE)
*
HSCSETV  EQU   *
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
         BR    R8
***********************************************************************
* CONSTANTS                                                           *
***********************************************************************
HEXTABLE DC    C'0123456789ABCDEF'
         LTORG
*
         HSCWAREA
         SLSXREQM
         IRXEFPL
         IRXARGTB
         IRXEVALB
         IKJTSVT
         YREGS
         END   HSC@QSCR
