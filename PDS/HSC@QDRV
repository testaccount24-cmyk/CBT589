HSC@QDRV CSECT
HSC@QDRV AMODE 31
HSC@QDRV RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING HSC@QDRV,R12
***********************************************************************
* QDRIVES  FUNCTION IMPLEMENTATION                                    *
***********************************************************************
         LR    R11,R1
         USING WORKAREA,R11
*
         SLSXREQ QDRIVES,                                              X
               OPTION=SYNCH,                                           X
               XCALADR=WXCALADR,                                       X
               MF=(E,SLSXPARM)
         LTR   R15,R15
         BNZ   HSCNOACT
         LR    R7,R1
         USING SLX,R7
         XR    R15,R15
         ICM   R15,B'0001',SLXCMDRC
         LA    R3,24
         CR    R15,R3
         BH    ERROINVL
         B     *+4(R15)
         B     CMDOK                   RC = 00 ?
         B     ERROQDRV                RC = 04 ?
         B     ERROPARM                RC = 08 ?
         B     ERROUNRC                RC = 12 ?
         B     ERROUNSC                RC = 16 ?
         B     HSCNOACT                RC = 20 ?
CMDOK    EQU   *
         L     R5,SLXXDRVN
         L     R4,SLXXDRVL
         L     R2,SLXXDRVO
         LA    R2,SLX(2)
         USING SLXSDRV,R2
         BAS   R6,SETVAR
         PR
ERROQDRV EQU   *
         LA    R15,4
         PR
ERROPARM EQU   *                       INVALID PARM SPECIFIED
         LA    R15,8
         PR
ERROUNRC EQU   *                       UNRECOVERABLE ERROR
         LA    R15,12
         PR
ERROUNSC EQU   *                       UNSUCCESSFUL REQUEST
         BAS   R6,SETVARER
         LA    R15,16
         PR
HSCNOACT EQU   *                       HSC NOT AVAILABLE
         LA    R15,20
         PR
ERRONAUT EQU   *                       FUNCTION NOT AUTHORIZED
         LA    R15,24
         PR
ERROINVL EQU   *                       INVALID RETURN CODE
         LA    R15,50
         PR
ERROSETV EQU   *                       SETVAR ROUTINE ERROR
         LA    R15,60
         PR
***********************************************************************
* SET VARIABLES SUBROUTINE                                            *
***********************************************************************
SETVAR   EQU   *
         ZAP   STEMCONT(2),=P'1'
*
* SET VARIABLE @SLXQDEAC.0 => NUMBER OF ENTRIES
*
         MVC   NAMEVAR(11),=C'@SLXQDEAC.0'
         MVC   SHVNAML(4),=F'11'
         CVD   R5,DECNUMBR
         UNPK  BCDNUMBR(2),DECNUMBR(8)
         OC    BCDNUMBR(2),=X'F0F0'
         MVC   VALUEVAR(2),BCDNUMBR
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV
NEXTSTEM EQU   *
*
* SET VARIABLE @SLXQDEAC => ACS ID
*
         MVC   NAMEVAR(12),=C'@SLXQDEAC.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTDEA1
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         B     CONTDEA2
CONTDEA1 EQU   *
         MVC   SHVNAML(4),=F'12'
         UNPK  NAMEVAR+10(2),STEMCONT(2)
         OC    NAMEVAR+10,=X'F0F0'
CONTDEA2 EQU   *
         MVC   HEXNUMBR(1),SLXQDEAC
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=2X'0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV
*
* SET VARIABLE @SLXQDELS => LSM ID
*
         MVC   NAMEVAR(12),=C'@SLXQDELS.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTDEL1
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         B     CONTDEL2
CONTDEL1 EQU   *
         MVC   SHVNAML(4),=F'12'
         UNPK  NAMEVAR+10(2),STEMCONT(2)
         OC    NAMEVAR+10,=X'F0F0'
CONTDEL2 EQU   *
         MVC   HEXNUMBR(1),SLXQDELS
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=2X'0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV
*
* SET VARIABLE @SLXQDECU => DRIVE ADDRESS
*
         MVC   NAMEVAR(12),=C'@SLXQDECU.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTDEC1
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         B     CONTDEC2
CONTDEC1 EQU   *
         MVC   SHVNAML(4),=F'12'
         UNPK  NAMEVAR+10(2),STEMCONT(2)
         OC    NAMEVAR+10,=X'F0F0'
CONTDEC2 EQU   *
         MVC   HEXNUMBR(2),SLXQDECU
         UNPK  BCDNUMBR(5),HEXNUMBR(3)
         NC    BCDNUMBR,=4X'0F'
         TR    BCDNUMBR(4),HEXTABLE
         MVC   VALUEVAR(4),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'4'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV
*
* SET VARIABLE @SLXQDRT => RECORDING TECHNIQUE OF DRIVE
*
         MVC   NAMEVAR(11),=C'@SLXQDRT.  '
         CP    STEMCONT(2),=P'9'
         BH    CONTDRT1
         MVC   SHVNAML(4),=F'10'
         UNPK  NAMEVAR+9(1),STEMCONT(2)
         OI    NAMEVAR+9,X'F0'
         B     CONTDRT2
CONTDRT1 EQU   *
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+9(2),STEMCONT(2)
         OC    NAMEVAR+9,=X'F0F0'
CONTDRT2 EQU   *
         MVC   VALUEVAR(8),SLXQDRT
         MVC   SHVVALL(4),=F'8'
         BAS   R8,HSCSETV
*
         LA    R2,SLXSDRVL(,R2)        NEXT DSN ELEMENT
         AP    STEMCONT(2),=P'1'
         BCT   R5,NEXTSTEM
*
* SET VARIABLE @SLXCMDRC => RETURN CODE
*
SETVARER EQU   *
         MVC   NAMEVAR(9),=C'@SLXCMDRC'
         MVC   SHVNAML(4),=F'9'
         MVC   HEXNUMBR(1),SLXCMDRC
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=X'0F0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV
*
* SET VARIABLE @SLXSRC => REASON CODE
*
         MVC   NAMEVAR(7),=C'@SLXSRC'
         MVC   SHVNAML(4),=F'7'
         MVC   HEXNUMBR(4),SLXSRC
         UNPK  BCDNUMBR(9),HEXNUMBR(5)
         NC    BCDNUMBR,=X'0F0F0F0F0F0F0F0F'
         TR    BCDNUMBR(8),HEXTABLE
         MVC   VALUEVAR(8),BCDNUMBR
         MVC   SHVVALL(4),=F'8'
         BAS   R8,HSCSETV
*
         BR    R6
*
*     CALL HSC@SETV (SET VARIABLE ROUTINE)
*
HSCSETV  EQU   *
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
         BR    R8
***********************************************************************
* CONSTANTS                                                           *
***********************************************************************
HEXTABLE DC    C'0123456789ABCDEF'
         LTORG
*
         HSCWAREA
         SLSXREQM
         IRXEFPL
         IRXARGTB
         IRXEVALB
         IKJTSVT
         YREGS
         END   HSC@QDRV
