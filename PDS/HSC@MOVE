HSC@MOVE CSECT
HSC@MOVE AMODE 31
HSC@MOVE RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING HSC@MOVE,R12
***********************************************************************
* MOVE    FUNCTION IMPLEMENTATION                                     *
***********************************************************************
         LR    R11,R1
         USING WORKAREA,R11
*
         L     R10,ARGTADDR
         USING ARGTABLE_ENTRY,R10
         LA    R10,8(R10)
         CLC   ARGTABLE_ARGSTRING_PTR(8),=XL8'FFFFFFFFFFFFFFFF'
         BE    ERROPARM
         L     R5,ARGTABLE_ARGSTRING_PTR
         L     R4,ARGTABLE_ARGSTRING_LENGTH
         CL    R4,=F'6'                IF VOLUME > 6 GOTO ERROPARM
         BH    ERROPARM
         BCTR  R4,0
         EX    R4,MOVEVOLM
*
         LA    R10,8(R10)
         CLC   ARGTABLE_ARGSTRING_PTR(8),=XL8'FFFFFFFFFFFFFFFF'
         BE    ERROPARM
         L     R5,ARGTABLE_ARGSTRING_PTR
         L     R4,ARGTABLE_ARGSTRING_LENGTH
         CL    R4,=F'4'                IF LSMID > 4 GOTO ERROPARM
         BH    ERROPARM
         BCTR  R4,0
         EX    R4,MOVEADDR
         TR    DRIVEADR(4),CHRTABLE
         PACK  DRIVEADR(4),DRIVEADR(4)
         L     R5,DRIVEADR
         SRL   R5,4
         STH   R5,TOLSMID
*
         SLSXREQ MOVE,                                                 X
               OPTION=SYNCH,                                           X
               VOLSER=VOLUME,                                          X
               TOLSM=TOLSMID,                                          X
               XCALADR=WXCALADR,                                       X
               MF=(E,SLSXPARM)
         LTR   R15,R15
         BNZ   HSCNOACT
         LR    R7,R1
         USING SLX,R7
         XR    R15,R15
         ICM   R15,B'0001',SLXCMDRC
         LA    R3,24
         CR    R15,R3
         BH    ERROINVL
         B     *+4(R15)
         B     CMDOK                   RC = 00 ?
         B     ERROMOVE                RC = 04 ?
         B     ERROPARM                RC = 08 ?
         B     ERROUNRC                RC = 12 ?
         B     ERROUNSC                RC = 16 ?
         B     HSCNOACT                RC = 20 ?
         B     ERRONAUT                RC = 24 ?
CMDOK    EQU   *
         BAS   R6,SETVAR
         PR
ERROMOVE EQU   *
         LA    R15,4
         PR
ERROPARM EQU   *                       INVALID PARM SPECIFIED
         LA    R15,8
         PR
ERROUNRC EQU   *                       UNRECOVERABLE ERROR
         LA    R15,12
         PR
ERROUNSC EQU   *                       UNSUCCESSFUL REQUEST
         BAS   R6,SETVAR
         LA    R15,16
         PR
HSCNOACT EQU   *                       HSC NOT AVAILABLE
         LA    R15,20
         PR
ERRONAUT EQU   *                       FUNCTION NOT AUTHORIZED
         LA    R15,24
         PR
ERROINVL EQU   *                       INVALID RETURN CODE
         LA    R15,50
         PR
ERROSETV EQU   *                       SETVAR ROUTINE ERROR
         LA    R15,60
         PR
***********************************************************************
* SET VARIABLES SUBROUTINE                                            *
***********************************************************************
SETVAR   EQU   *
*
* SET VARIABLE @SLXCMDRC => RETURN CODE
*
         MVC   NAMEVAR(9),=C'@SLXCMDRC'
         MVC   SHVNAML(4),=F'9'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   HEXNUMBR(1),SLXCMDRC
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=X'0F0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR
         MVC   SHVVALL(4),=F'2'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
*
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
*
* SET VARIABLE @SLXSRC => REASON CODE
*
         MVC   NAMEVAR(7),=C'@SLXSRC'
         MVC   SHVNAML(4),=F'7'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   HEXNUMBR(4),SLXSRC
         UNPK  BCDNUMBR(9),HEXNUMBR(5)
         NC    BCDNUMBR,=X'0F0F0F0F0F0F0F0F'
         TR    BCDNUMBR(8),HEXTABLE
         MVC   VALUEVAR(8),BCDNUMBR
         MVC   SHVVALL(4),=F'8'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
*
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
         BR    R6
*
MOVEADDR MVC   DRIVEADR(0),0(R5)
MOVEVOLM MVC   VOLUME(0),0(R5)
***********************************************************************
* CONSTANTS                                                           *
***********************************************************************
HEXTABLE DC    C'0123456789ABCDEF'
CHRTABLE DC    X'00000000000000000000000000000000'   00-0F
         DC    X'00000000000000000000000000000000'   10-1F
         DC    X'00000000000000000000000000000000'   20-2F
         DC    X'00000000000000000000000000000000'   30-3F
         DC    X'00000000000000000000000000000000'   40-4F
         DC    X'00000000000000000000000000000000'   50-5F
         DC    X'00000000000000000000000000000000'   60-6F
         DC    X'00000000000000000000000000000000'   70-7F
         DC    X'000A0B0C0D0E0F000000000000000000'   80-8F
         DC    X'00000000000000000000000000000000'   90-9F
         DC    X'00000000000000000000000000000000'   A0-AF
         DC    X'00000000000000000000000000000000'   B0-BF
         DC    X'000A0B0C0D0E0F000000000000000000'   C0-CF
         DC    X'00000000000000000000000000000000'   D0-DF
         DC    X'00000000000000000000000000000000'   E0-EF
         DC    X'00010203040506070809000000000000'   F0-FF
         LTORG
*
         HSCWAREA
         SLSXREQM
         IRXEFPL
         IRXARGTB
         IRXEVALB
         YREGS
         END   HSC@MOVE
