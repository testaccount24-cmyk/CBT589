***********************************************************************
* PROGRAMA: ONDESTOU                                                  *
*                                                                     *
* AUTOR: PHILIPPE LEITE                                               *
*                                                                     *
* FUNCTION: RETORNA AO PROGRAMA CHAMADOR UM RC INDICANDO O            *
*           AMBIENTE DE EXECUCAO.                                     *
*           RC = 1  -> BATCH                                          *
*           RC = 2  -> ONLINE (CICS)                                  *
*           RC = 3  -> CICS BATCH ???????? :P                         *
*           RC = 4  -> TSO                                            *
*           RC = 5  -> TSO BATCH                                      *
*                                                                     *
* OBS.: ESTE PROGRAMA PODE E DEVE SER LINKEDITADO COMO REENTRANT      *
*                                                                     *
***********************************************************************
ONDESTOU CSECT
ONDESTOU AMODE 31
ONDESTOU RMODE ANY
         BAKR  R14,0              SALVA REGISTRADORES
         USING ONDESTOU,R12       ENDERECAMENTO BASE
         LR    R12,R15            CARRAGA ENTRY POINT
         LA    R10,0              ZERA R10
         USING PSA,R10            BASE PARA PSA
         L     R10,PSAAOLD        CARREGA END. ASCB
         DROP  R10                DROPA R10
         USING ASCB,R10           BASE PARA ASCB
         L     R10,ASCBASXB       CARREGA END. ASXB
         DROP  R10                DROPA R10
         USING ASXB,R10           BASE PARA ASXB
         L     R2,ASXBFTCB        CARREGA END. PRIMEIRA TCB
         USING TCB,R2             BASE PARA TCB
         USING RBBASIC,R3         BASE PARA RB
         USING CDENTRY,R4         BASE PARA CDE
         XR    R8,R8              ZERA R8
         XR    R9,R9              ZERA R9
LOOPTCBQ EQU   *
         L     R3,TCBRBP          CARREGA END. RB EM R3
         ICM   R4,B'0111',RBCDE1  CARREGA END. CDE EM R4
         CLC   CDNAME,=C'IEAVAR00' EH REGION CONTROL TASK ?
         BE    NEXTTCB             SIM, VAI PARA PROX. TCB
         CLC   CDNAME,=C'IEAVTDST' EH DUMP TASK ?
         BE    NEXTTCB             SIM, VAI PARA PROX. TCB
         CLC   CDNAME,=C'IEFSD060' EH INITIATOR TASK ?
         BE    NEXTTCB             SIM, VAI PARA PROX. TCB
*
         CLC   CDNAME,=CL8'IEFIIC' EH BATCH INITIATOR ?
         BNE   CONTIIC             NAO, CONTINUA
*        WTO   'BATCH INITIATOR',ROUTCDE=11
         IC    R8,=X'01'          MARCA FLAG EM R8
         OR    R9,R8
         B     NEXTTCB            VAI PARA PROX. TCB
CONTIIC  EQU   *
         CLC   CDNAME,=CL8'DFHSIP' EH CICS ?
         BNE   CONTSIP             NAO, CONTINUA
*        WTO   'CICS',ROUTCDE=11
         IC    R8,=X'02'          MARCA FLAG EM R8
         OR    R9,R8
         B     NEXTTCB            VAI PARA PROX. TCB
CONTSIP  EQU   *
         CLC   CDNAME,=C'IKJEFT01' EH TSO ?
         BNE   CONTIKJ             NAO, CONTINUA
*        WTO   'TSO',ROUTCDE=11
         IC    R8,=X'04'          MARCA FLAG EM R8
         OR    R9,R8
CONTIKJ  EQU   *
NEXTTCB  EQU   *
         L     R2,TCBTCB          CARREGA END. PROX. TCB
         LTR   R2,R2              TESTA SE EH ZERO
         BZ    ENDLOOP            SIM, FIM DE PROGRAMA
         B     LOOPTCBQ
ENDLOOP  EQU   *
         LR    R15,R9             CARREGA RC EM R15
         PR    ,                  RETORNA AO OS
         LTORG
         IHAPSA
         IHAASCB
         IHAASXB
         IKJTCB
         IHARB
         IHACDE
         YREGS
         END   ONDESTOU
