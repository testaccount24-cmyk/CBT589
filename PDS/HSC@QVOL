HSC@QVOL CSECT
HSC@QVOL AMODE 31
HSC@QVOL RMODE ANY
         BAKR  R14,0                   LINKAGE STACK
         LR    R12,R15                 R12 IS THE BASE
         USING HSC@QVOL,R12
***********************************************************************
* QVOLUME FUNCTION IMPLEMENTATION                                     *
*                                                                     *
* 12/12/03  Tom Ritter  Used equates from STK macros                  *
***********************************************************************
         LR    R11,R1
         USING WORKAREA,R11
*
         L     R10,ARGTADDR
         USING ARGTABLE_ENTRY,R10
         LA    R10,8(R10)
         CLC   ARGTABLE_ARGSTRING_PTR(8),=XL8'FFFFFFFFFFFFFFFF'
         BE    ERROPARM
         L     R5,ARGTABLE_ARGSTRING_PTR
         L     R4,ARGTABLE_ARGSTRING_LENGTH
         CL    R4,=F'6'                IF VOLUME > 6 GOTO ERROPARM
         BH    ERROPARM
         BCTR  R4,0
         EX    R4,MOVEVOLM
*
         SLSXREQ QVOLUME,                                              X
               OPTION=SYNCH,                                           X
               VOLSER=VOLUME,                                          X
               XCALADR=WXCALADR,                                       X
               MF=(E,SLSXPARM)
         LTR   R15,R15
         BNZ   HSCNOACT
         LR    R7,R1
         USING SLX,R7
         ICM   R15,B'0001',SLXCMDRC
         LA    R3,24
         CR    R15,R3
         BH    ERROINVL
         B     *+4(R15)
         B     CMDOK                   RC = 00 ?
         B     ERROQVOL                RC = 04 ?
         B     ERROPARM                RC = 08 ?
         B     ERROUNRC                RC = 12 ?
         B     ERROUNSC                RC = 16 ?
         B     HSCNOACT                RC = 20 ?
CMDOK    EQU   *
         L     R4,SLXXVOLL
         L     R2,SLXXVOLO
         LA    R2,SLX(2)
         USING SLXSVOL,R2
         BAS   R6,SETVAR
         SLR   R15,R15
         PR
ERROQVOL EQU   *
         BAS   R6,SETVARER
         LA    R15,4
         PR
ERROPARM EQU   *                       INVALID PARM SPECIFIED
         LA    R15,8
         PR
ERROUNRC EQU   *                       UNRECOVERABLE ERROR
         LA    R15,12
         PR
ERROUNSC EQU   *                       UNSUCCESSFUL REQUEST
         BAS   R6,SETVARER
         LA    R15,16
         PR
HSCNOACT EQU   *                       HSC NOT AVAILABLE
         LA    R15,20
         PR
ERRONAUT EQU   *                       FUNCTION NOT AUTHORIZED
         LA    R15,24
         PR
ERROINVL EQU   *                       INVALID RETURN CODE
         LA    R15,50
         PR
ERROSETV EQU   *                       SETVAR ROUTINE ERROR
         LA    R15,60
         PR
***********************************************************************
* SET VARIABLES SUBROUTINE                                            *
***********************************************************************
SETVAR   EQU   *
*
* SET VARIABLE @SLXVILB => IN LIBRARY
*
         MVC   NAMEVAR(8),=C'@SLXVILB' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'        SET VARIABLE NAME LENGTH
         TM    SLXVSTA,SLXVILB      VOLUME IS IN LIBRAY ?
         BNO   CONTVILB
         MVC   VALUEVAR(3),=C'YES'  SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'3'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
         B     STVOHST
CONTVILB EQU   *
         MVC   VALUEVAR(2),=C'NO'   SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVOHST => IN USE BY ANOTHER HOST
*
STVOHST  EQU   *
         MVC   NAMEVAR(9),=C'@SLXVOHST' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'9'         SET VARIABLE NAME LENGTH
         TM    SLXVSTA,SLXVOHST     VOLUME IS IN USE BY ANOTHER HOST ?
         BNO   CONTOHST
         MVC   VALUEVAR(3),=C'YES'  SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'3'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
         B     STVSCR
CONTOHST EQU   *
         MVC   VALUEVAR(2),=C'NO'   SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVSCR  => SCRATCH
*
STVSCR   EQU   *
         MVC   NAMEVAR(8),=C'@SLXVSCR' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'        SET VARIABLE NAME LENGTH
         TM    SLXVSTA,SLXVSCR      VOLUME IS CONSIDERED SCRATCH ?
         BNO   CONTSCR
         MVC   VALUEVAR(3),=C'YES'  SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'3'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
         B     STVMAL
CONTSCR  EQU   *
         MVC   VALUEVAR(2),=C'NO'   SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVMAL  => MANUAL MODE LSM
*
STVMAL   EQU   *
         MVC   NAMEVAR(8),=C'@SLXVMAL' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'        SET VARIABLE NAME LENGTH
         TM    SLXVSTA,SLXVMAL      VOLUME IS IN MANUAL-MODE LSM ?
         BNO   CONTMAL
         MVC   VALUEVAR(3),=C'YES'  SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'3'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
         B     STVDSC
CONTMAL  EQU   *
         MVC   VALUEVAR(2),=C'NO'   SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVDSC  => DISCONNECTED ACS
*
STVDSC   EQU   *
         MVC   NAMEVAR(8),=C'@SLXVDSC' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'        SET VARIABLE NAME LENGTH
         TM    SLXVSTA,SLXVDSC      VOLUME IS IN DISCONNECTED ACS ?
         BNO   CONTDSC
         MVC   VALUEVAR(3),=C'YES'  SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'3'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
         B     STVMNT
CONTDSC  EQU   *
         MVC   VALUEVAR(2),=C'NO'   SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVMNT  => MOUNTED ON A DRIVE
*
STVMNT   EQU   *
         MVC   NAMEVAR(8),=C'@SLXVMNT' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'        SET VARIABLE NAME LENGTH
         TM    SLXVSTA,SLXVMNT      VOLUME IS MOUNTED ON A DRIVE ?
         BNO   CONTMNT
         MVC   VALUEVAR(3),=C'YES'  SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'3'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
         B     STVERR
CONTMNT  EQU   *
         MVC   VALUEVAR(2),=C'NO'   SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVERR  => VOLUME ERRANT
*
STVERR   EQU   *
         MVC   NAMEVAR(8),=C'@SLXVERR' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'        SET VARIABLE NAME LENGTH
         TM    SLXVSTA,SLXVERR      VOLUME IS 'ERRANT' ?
         BNO   CONTERR
         MVC   VALUEVAR(3),=C'YES'  SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'3'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
         B     STVACS
CONTERR  EQU   *
         MVC   VALUEVAR(2),=C'NO'   SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVACS  => ACS ID
*
STVACS   EQU   *
         MVC   NAMEVAR(8),=C'@SLXVACS' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'        SET VARIABLE NAME LENGTH
         MVC   HEXNUMBR(1),SLXVACS
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=2X'0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVLSM  => LSM ID
*
         MVC   NAMEVAR(8),=C'@SLXVLSM' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'        SET VARIABLE NAME LENGTH
         MVC   HEXNUMBR(1),SLXVLSM
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=2X'0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVPNL  => PANEL
*
         CLI   SLXVLC,X'02'         LOCATION DATA IS A DRIVE ADDRESS ?
         BE    SETDRIVE
         MVC   NAMEVAR(8),=C'@SLXVPNL' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'     SET VARIABLE NAME LENGTH
         SLR   R5,R5
         ICM   R5,B'0001',SLXVPNL
         CVD   R5,DECNUMBR
         UNPK  BCDNUMBR(2),DECNUMBR(8)
         OC    BCDNUMBR(2),=X'F0F0'
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVROW  => ROW
*
         MVC   NAMEVAR(8),=C'@SLXVROW' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'     SET VARIABLE NAME LENGTH
         SLR   R5,R5
         ICM   R5,B'0001',SLXVPNL+1
         CVD   R5,DECNUMBR
         UNPK  BCDNUMBR(2),DECNUMBR(8)
         OC    BCDNUMBR(2),=X'F0F0'
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXVCOL  => COLUMN
*
         MVC   NAMEVAR(8),=C'@SLXVCOL' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'8'        SET VARIABLE NAME LENGTH
         SLR   R5,R5
         ICM   R5,B'0001',SLXVPNL+2
         CVD   R5,DECNUMBR
         UNPK  BCDNUMBR(2),DECNUMBR(8)
         OC    BCDNUMBR(2),=X'F0F0'
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
         B     SETVARER
*
* SET VARIABLE @SLXVDRIV => DRIVE ADDRESS
*
SETDRIVE EQU   *
         MVC   NAMEVAR(9),=C'@SLXVDRIV' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'9'         SET VARIABLE NAME LENGTH
         MVC   HEXNUMBR(2),SLXVDRIV
         UNPK  BCDNUMBR(5),HEXNUMBR(3)
         NC    BCDNUMBR,=4X'0F'
         TR    BCDNUMBR(4),HEXTABLE
         MVC   VALUEVAR(4),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'4'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
SETVARER EQU   *
*
* SET VARIABLE @SLXCMDRC => RETURN CODE
*
         MVC   NAMEVAR(9),=C'@SLXCMDRC' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'9'         SET VARIABLE NAME LENGTH
         MVC   HEXNUMBR(1),SLXCMDRC
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=X'0F0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'2'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
*
* SET VARIABLE @SLXSRC => REASON CODE
*
         MVC   NAMEVAR(7),=C'@SLXSRC' SET VARIABLE NAME
         MVC   SHVNAML(4),=F'7'       SET VARIABLE NAME LENGTH
         MVC   HEXNUMBR(4),SLXSRC
         UNPK  BCDNUMBR(9),HEXNUMBR(5)
         NC    BCDNUMBR,=X'0F0F0F0F0F0F0F0F'
         TR    BCDNUMBR(8),HEXTABLE
         MVC   VALUEVAR(8),BCDNUMBR SET VARIABLE VALUE
         MVC   SHVVALL(4),=F'8'     SET VARIABLE LENGTH
         BAS   R8,HSCSETV           CALL HSC@SETV
         BR    R6
*
*     CALL HSC@SETV (SET VARIABLE ROUTINE)
*
HSCSETV  EQU   *
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
         BR    R8
*
MOVEVOLM MVC   VOLUME(0),0(R5)
***********************************************************************
* CONSTANTS                                                           *
***********************************************************************
HEXTABLE DC    C'0123456789ABCDEF'
         LTORG
*
         HSCWAREA
         SLSXREQM
         IRXEFPL
         IRXARGTB
         IRXEVALB
         IKJTSVT
         YREGS
         END   HSC@QVOL
