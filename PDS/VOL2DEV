VOL2DEV  CSECT
VOL2DEV  AMODE 31
VOL2DEV  RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING VOL2DEV,R12
         MODID ,
**********************************************************************
* PROGRAM: VOL2DEV                                                   *
*                                                                    *
* AUTHOR: PHILIPPE LEITE                                             *
*                                                                    *
* OBJECTIVE: REXX EXTERNAL FUNCTION TO CONVERT VOLUME SERIAL TO      *
*            DEVICE NUMBER.                                          *
*                                                                    *
* VARIABLES RETURNED:                                                *
*                                                                    *
*   @VOL2DEV - DEVICE NUMBER                                         *
*                                                                    *
*  POSSIBLE RETURN CODES:                                            *
*                                                                    *
*   00 - SUCCESSFUL EXECUTION                                        *
*   04 - UCB NOT FOUND                                               *
*   16 - INVALID PARAMETER                                           *
*                                                                    *
* EXAMPLE:                                                           *
*                                                                    *
*      /* REXX */                                                    *
*      RC = VOL2DEV('SHR010')                                        *
*      IF RC = 0 THEN SAY @VOL2DEV                                   *
*                                                                    *
* CREATION DATE: 27/05/2004                                          *
*                                                                    *
**********************************************************************
         LR    R7,R0
         LR    R9,R1
         USING EFPL,R9
*
         STORAGE OBTAIN,LENGTH=WAREALEN
         LR    R8,R1
         USING WKAREA,R8
*
         USING ENVBLOCK,R7          SET ADDRESSABILITY TO ENVBLOCK
         L     R6,ENVBLOCK_IRXEXTE  EXTERNAL ENTRY VECTOR ADDRESS
         USING IRXEXTE,R6           SET ADDRESSABILITY TO EXTE
         L     R0,IRXEXCOM          IRXEXCOM ENTRY ADDRESS
         ST    R0,IRXEXCO@
         DROP  R6
         DROP  R7
*
         L     R10,EFPLARG
         USING ARGTABLE_ENTRY,R10
         L     R5,ARGTABLE_ARGSTRING_PTR
         L     R4,ARGTABLE_ARGSTRING_LENGTH
         CL    R4,=F'6'
         BNH   CONTMOVV
         MVC   RETRCODE,=F'16'
         B     CONVRETR
CONTMOVV EQU   *
         MVC   VOLUME,=CL6' '
         BCTR  R4,0
         EX    R4,MOVEVOL
*
         UCBSCAN COPY,                                                 X
               VOLSER=VOLUME,                                          X
               DEVNCHAR=DEVICE,                                        X
               UCBAREA=UCBCOPY,                                        X
               WORKAREA=SCANWORK,                                      X
               DYNAMIC=YES,                                            X
               NONBASE=NO,                                             X
               RANGE=ALL,                                              X
               DEVCLASS=DASD,                                          X
               MF=(E,UCBSCANL)
         ST    R15,RETRCODE
*
CONVRETR EQU   *
         L     R11,EFPLEVAL
         L     R11,0(R11)
         USING EVALBLOCK,R11
         L     R5,RETRCODE
         CVD   R5,PACKCODE
         UNPK  EBCDCODE,PACKCODE
         OC    EBCDCODE,=2X'F0'
         MVC   EVALBLOCK_EVLEN,=F'2'
         MVC   EVALBLOCK_EVDATA(2),EBCDCODE
         LTR   R5,R5
         BNZ   ENDV2D
*
CONTSETV EQU   *
         LA    R7,PARAM4
         USING SHVBLOCK,R7
         MVC   NAMEVAR,=C'@VOL2DEV'    SET VARIABLE NAME
         MVC   SHVNAML,=F'8'           SET VARIABLE NAME LENGTH
         MVC   SHVVALL,=F'4'           SET VARIABLE LENGTH
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         LA    R15,DEVICE
         ST    R15,SHVVALA
         BAS   R6,DEV@SETV
*
ENDV2D   EQU   *
         STORAGE RELEASE,LENGTH=WAREALEN,ADDR=(R8)
         XR    R15,R15
         PR
DEV@SETV EQU   *
         MVC   PARAM1,=C'IRXEXCOM'
         MVI   SHVCODE,C'S'
         L     R15,IRXEXCO@
         CALL  (15),(PARAM1,0,0,PARAM4),VL,MF=(E,EXCOPLST)
         BR    R6
*
MOVEVOL  MVC   VOLUME(0),0(R5)
         YREGS
         LTORG
*---------------------------------------------------------------------*
*  DSECT WORK AREA                                                    *
*---------------------------------------------------------------------*
WKAREA   DSECT
PACKCODE DS    PL8
EBCDCODE DS    CL2
RETRCODE DS    F
IRXEXCO@ DS    F
EXCOPLST DS    4F
NAMEVAR  DS    CL8
DEVICE   DS    CL4
VOLUME   DS    CL6
UCBCOPY  DS    CL48
SCANWORK DS    CL100
* IRXEXCOM PARAMETER AREA
PARAM1   DS    CL8
PARAM2   DS    A
PARAM3   DS    A
PARAM4   DS    XL32
         UCBSCAN MF=(L,UCBSCANL)
WAREALEN EQU   *-WKAREA
*---------------------------------------------------------------------*
*  DSECTS AREA                                                        *
*---------------------------------------------------------------------*
         IRXEFPL
         IRXENVB
         IRXEXTE
         IRXSHVB
         IRXARGTB
         IRXEVALB
         IEFUCBOB DEVCLAS=DA
         END   VOL2DEV
