IGC0023A CSECT
IGC0023A AMODE 31
IGC0023A RMODE ANY
         USING IGC0023A,R6
         LR    R2,R14             SAVE RETURN ADDRESS
         LR    R12,R0             LOAD SVC OPTION
         LR    R10,R1             LOAD PARM ADDRESS
         USING PARMAREA,R10
         STORAGE OBTAIN,LENGTH=WAREALEN
         LR    R11,R1
         USING WORKAREA,R11
*
         LTR   R12,R12
         BNZ   VOL2DEV
***********************************************************************
* CONVERT DEVICE NUMBER TO VOLUME SERIAL                              *
***********************************************************************
         UCBLOOK DEVNCHAR=DEVICE,                                      X
               UCBPTR=UCB,                                             X
               UCBCXPTR=UCBX,                                          X
               DYNAMIC=YES,                                            X
               LOC=ANY,                                                X
               RANGE=ALL,                                              X
               NOPIN,MF=(E,UCBDL)
         LTR   R15,R15
         BNZ   SVCEND04
         L     R8,UCB             COMMON SEGMENT ADDRESS
         USING UCBCMSEG,R8
         TM    UCBTBYT3,UCB3DACC  IS A DASD DEVICE ?
         BNO   SVCEND12
         TM    UCBSTAT,UCBONLI    IS DEVICE ONLINE ?
         BNO   SVCEND08
         L     R9,UCBX
         USING UCBCMEXT,R9
         MVC   VOLSER,UCBVOLI
         B     SVCENDOK
***********************************************************************
* CONVERT VOLUME SERIAL TO DEVICE NUMBER                              *
***********************************************************************
VOL2DEV  EQU   *
         UCBLOOK VOLSER=VOLSER,                                        X
               UCBPTR=UCB,                                             X
               UCBCXPTR=UCBX,                                          X
               DYNAMIC=YES,                                            X
               DEVCLASS=DASD,                                          X
               LOC=ANY,                                                X
               RANGE=ALL,                                              X
               NOPIN,MF=(E,UCBDL)
         LTR   R15,R15
         BNZ   SVCEND04
         L     R8,UCB
         USING UCBCMSEG,R8
         UNPK  MYDEV(L'MYDEV+1),UCBCHAN(L'UCBCHAN+1)
         NC    MYDEV,=4X'0F'
         TR    MYDEV,HEXTABLE
         MVC   DEVICE,MYDEV
         B     SVCENDOK
*
SVCEND   EQU   *
         STORAGE RELEASE,LENGTH=WAREALEN,ADDR=(R11)
         LR    R15,R7             LOAD RETURN CODE
         LR    R14,R2             LOAD RETURN ADDRESS
         BR    R14
SVCEND12 EQU   *
         LA    R7,12
         B     SVCEND
SVCEND08 EQU   *
         LA    R7,8
         B     SVCEND
SVCEND04 EQU   *
         LA    R7,4
         B     SVCEND
SVCENDOK EQU   *
         LA    R7,0
         B     SVCEND
*
HEXTABLE DC    C'0123456789ABCDEF'
         LTORG
PARMAREA DSECT
DEVICE   DS    CL4
VOLSER   DS    CL6
*
WORKAREA DSECT
MYDEV    DS    CL4
         DS    CL1
UCB      DS    F
UCBX     DS    F
UCBL     UCBLOOK MF=(L,UCBDL)
WAREALEN EQU   *-WORKAREA
*
         YREGS
         CVT   DSECT=YES
         IEFUCBOB
         END   IGC0023A
