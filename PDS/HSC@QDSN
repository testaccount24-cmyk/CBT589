HSC@QDSN CSECT
HSC@QDSN AMODE 31
HSC@QDSN RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING HSC@QDSN,R12
***********************************************************************
* QDSN  FUNCTION IMPLEMENTATION                                       *
***********************************************************************
         LR    R11,R1
         USING WORKAREA,R11
*
         SLSXREQ QDSN,                                                 X
               OPTION=SYNCH,                                           X
               XCALADR=WXCALADR,                                       X
               MF=(E,SLSXPARM)
         LTR   R15,R15
         BNZ   HSCNOACT
         LR    R7,R1
         USING SLX,R7
         XR    R15,R15
         ICM   R15,B'0001',SLXCMDRC
         LA    R3,24
         CR    R15,R3
         BH    ERROINVL
         B     *+4(R15)
         B     CMDOK                   RC = 00 ?
         B     ERROQDSN                RC = 04 ?
         B     ERROPARM                RC = 08 ?
         B     ERROUNRC                RC = 12 ?
         B     ERROUNSC                RC = 16 ?
         B     HSCNOACT                RC = 20 ?
CMDOK    EQU   *
         L     R5,SLXXQDSN
         L     R4,SLXXQDSL
         L     R2,SLXXQDSO
         LA    R2,SLX(2)
         USING SLXDSNIM,R2
         BAS   R6,SETVAR
         PR
ERROQDSN EQU   *
         LA    R15,4
         PR
ERROPARM EQU   *                       INVALID PARM SPECIFIED
         LA    R15,8
         PR
ERROUNRC EQU   *                       UNRECOVERABLE ERROR
         LA    R15,12
         PR
ERROUNSC EQU   *                       UNSUCCESSFUL REQUEST
         BAS   R6,SETVARER
         LA    R15,16
         PR
HSCNOACT EQU   *                       HSC NOT AVAILABLE
         LA    R15,20
         PR
ERRONAUT EQU   *                       FUNCTION NOT AUTHORIZED
         LA    R15,24
         PR
ERROINVL EQU   *                       INVALID RETURN CODE
         LA    R15,50
         PR
ERROSETV EQU   *                       SETVAR ROUTINE ERROR
         LA    R15,60
         PR
***********************************************************************
* SET VARIABLES SUBROUTINE                                            *
***********************************************************************
SETVAR   EQU   *
         ZAP   STEMCONT(2),=P'1'
*
* SET VARIABLE @SLXDSNAM.0 => NUMBER OF ENTRIES
*
         MVC   NAMEVAR(11),=C'@SLXDSNAM.0'
         MVC   SHVNAML(4),=F'11'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         CVD   R5,DECNUMBR
         UNPK  BCDNUMBR(1),DECNUMBR(8)
         OI    BCDNUMBR,X'F0'
         MVC   VALUEVAR(1),BCDNUMBR
         MVC   SHVVALL(4),=F'1'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
NEXTSTEM EQU   *
*
* SET STEM @SLXDSNAM. => DATA SET NAME
*
         MVC   NAMEVAR(11),=C'@SLXDSNAM. '
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   VALUEVAR(44),SLXDSNAM
         MVC   SHVVALL(4),=F'44'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
*
* SET STEM @SLXDSMBR. => MEMBER NAME
*
         MVC   NAMEVAR(11),=C'@SLXDSMBR. '
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   VALUEVAR(8),SLXDSMBR
         MVC   SHVVALL(4),=F'8'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
*
* SET STEM @SLXDSTYP. => DATA SET TYPE
*
         MVC   NAMEVAR(11),=C'@SLXDSTYP. '
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
*
         CLI   SLXDSFLG,SLXDSPRM       PRIMARY ?
         BNE   CONTPRM
         MVC   VALUEVAR(20),=CL20'CDS PRIMARY'
         MVC   SHVVALL(4),=F'20'
         B     CONTJNA
CONTPRM  EQU   *
         CLI   SLXDSFLG,SLXDSSEC       SECONDARY ?
         BNE   CONTSEC
         MVC   VALUEVAR(20),=CL20'CDS SECONDARY'
         MVC   SHVVALL(4),=F'20'
         B     CONTJNA
CONTSEC  EQU   *
         CLI   SLXDSFLG,SLXDSSBY       STANDBY ?
         BNE   CONTSBY
         MVC   VALUEVAR(20),=CL20'CDS STANDBY'
         MVC   SHVVALL(4),=F'20'
         B     CONTJNA
CONTSBY  EQU   *
         CLI   SLXDSFLG,SLXDSVAT       VOLUME ATTRIBUTES ?
         BNE   CONTVAT
         MVC   VALUEVAR(20),=CL20'VOLUME ATTRIBUTES'
         MVC   SHVVALL(4),=F'20'
         B     CONTJNA
CONTVAT  EQU   *
         CLI   SLXDSFLG,SLXDSUAT       UNIT ATTRIBUTES ?
         BNE   CONTUAT
         MVC   VALUEVAR(20),=CL20'UNIT ATTRIBUTES'
         MVC   SHVVALL(4),=F'20'
         B     CONTJNA
CONTUAT  EQU   *
         CLI   SLXDSFLG,SLXDSTRQ       TAPEREQS ?
         BNE   CONTTRQ
         MVC   VALUEVAR(20),=CL20'TAPEREQS'
         MVC   SHVVALL(4),=F'20'
         B     CONTJNA
CONTTRQ  EQU   *
         CLI   SLXDSFLG,SLXDSPLB       PARMLIB ?
         BNE   CONTPLB
         MVC   VALUEVAR(20),=CL20'PARMLIB'
         MVC   SHVVALL(4),=F'20'
         B     CONTJNA
CONTPLB  EQU   *
         CLI   SLXDSFLG,SLXDSJNP       PRIMARY JOURNAL ?
         BNE   CONTJNP
         MVC   VALUEVAR(20),=CL20'PRIMARY JOURNAL'
         MVC   SHVVALL(4),=F'20'
         B     CONTJNA
CONTJNP  EQU   *
         CLI   SLXDSFLG,SLXDSJNA       ALTERNATE JOURNAL ?
         BNE   CONTJNA
         MVC   VALUEVAR(20),=CL20'ALTERNATE JOURNAL'
         MVC   SHVVALL(4),=F'20'
CONTJNA  EQU   *
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
*
* SET STEM @SLXDSVOL. => VOLUME NAME
*
         MVC   NAMEVAR(11),=C'@SLXDSVOL. '
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   VALUEVAR(6),SLXDSVOL
         MVC   SHVVALL(4),=F'6'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
*
* SET STEM @SLXDSUNT. => UNIT NAME
*
         MVC   NAMEVAR(11),=C'@SLXDSUNT. '
         MVC   SHVNAML(4),=F'11'
         UNPK  NAMEVAR+10(1),STEMCONT(2)
         OI    NAMEVAR+10,X'F0'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   VALUEVAR(8),SLXDSUNT
         MVC   SHVVALL(4),=F'8'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
*
         LA    R2,SLXDSNEL(,R2)        NEXT DSN ELEMENT
         AP    STEMCONT(2),=P'1'
         BCT   R5,NEXTSTEM
*
SETVARER EQU   *
*
* SET VARIABLE @SLXCMDRC => RETURN CODE
*
         MVC   NAMEVAR(9),=C'@SLXCMDRC'
         MVC   SHVNAML(4),=F'9'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   HEXNUMBR(1),SLXCMDRC
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=X'0F0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR
         MVC   SHVVALL(4),=F'2'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
*
* SET VARIABLE @SLXSRC => REASON CODE
*
         MVC   NAMEVAR(7),=C'@SLXSRC'
         MVC   SHVNAML(4),=F'7'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   HEXNUMBR(4),SLXSRC
         UNPK  BCDNUMBR(9),HEXNUMBR(5)
         NC    BCDNUMBR,=X'0F0F0F0F0F0F0F0F'
         TR    BCDNUMBR(8),HEXTABLE
         MVC   VALUEVAR(8),BCDNUMBR
         MVC   SHVVALL(4),=F'8'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
*
         BR    R6
***********************************************************************
* CONSTANTS                                                           *
***********************************************************************
HEXTABLE DC    C'0123456789ABCDEF'
         LTORG
*
         HSCWAREA
         SLSXREQM
         IRXEFPL
         IRXARGTB
         IRXEVALB
         IKJTSVT
         YREGS
         END   HSC@QDSN
