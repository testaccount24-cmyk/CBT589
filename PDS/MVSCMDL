MVSCMDL  CSECT
MVSCMDL  AMODE 31
MVSCMDL  RMODE 24
         BAKR  R14,0               LINKAGE STACK
         LR    R12,R15             CARREGA ENTRY POINT EM R12
         USING MVSCMDL,R12         ESTABELECE ENDERECAMENTO
         MODID ,                   EYE-CATCHER E DATA
**********************************************************************
* PROGRAM: MVSCMDL                                                   *
*                                                                    *
* AUTHOR: PHILIPPE LEITE                                             *
*                                                                    *
* FUNCAO: BATCH PROGRAM TO ISSUE SYSTEM COMMANDS AND CAPTURE THE     *
*         RESULTS.                                                   *
*         THIS PROGRAM MUST LINKEDITED IN AN APF-AUTHORIZES LIBRARY  *
*         AND AC=1.                                                  *
*                                                                    *
* EXAMPLE:                                                           *
*                                                                    *
*  //JOBNAME JOB (P,SI,XXXXXX),CLASS=A,MSGCLASS=X,MSGLEVEL=(1,1)     *
*  //*                                                               *
*  //ST001 EXEC PGM=MVSCMDL                                          *
*  //CMDLOG   DD SYSOUT=*                                            *
*  //CMDIN    DD *                                                   *
*  D U,DASD,ONLINE                                                   *
*  R %%,YES                                                          *
*                                                                    *
*                                                                    *
* CREATION DATE: 19/04/2001                                          *
*                                                                    *
* HISTORY:                                                           *
*                                                                    *
*   21/05/2001 - CORRECAO PARA CAPTURAR MAIS DE UM MDB               *
*   15/01/2003 - CORRECAO BUG QUE CAPTURAVA MENSAGENS INDEVIDAS      *
*   15/01/2003 - ACRESCENTADO CONTADOR PARA CART                     *
*                                                                    *
**********************************************************************
**********************************************************************
* CORPO PRINCIPAL DO PROGRAMA                                        *
**********************************************************************
         BAS   R6,ABREFICH         ABRE FICHEIROS
         BAS   R6,ESPPARAM         ESPECIF. CARACTERIST. DA CONSOLE
         BAS   R6,ATIVAMCS         ATIVA MCS CONSOLE
LEPROXCO BAS   R6,LECOMAND         LE COMANDO DO FICHEIRO CMDIN
         BAS   R6,EMITECOM         EMITE COMANDO
         BAS   R6,AGUARESP         AGUARDA RESPOSTA
         BAS   R6,CAPTRESP         CAPTURA RESPOSTA DO COMANDO
         BAS   R6,LOGACMDS         POE RESULTADO EM CMDLOG
         B     LEPROXCO            LE O COMANDO SEGUINTE
DESMCSCO BAS   R6,DESATMCS         DESATIVA MCS CONSOLE
         BAS   R6,FECHFICH         FECHA OS FICHEIROS
         B     FIMPROG             FIM DO PROGRAMA
**********************************************************************
* ABRE  FICHEIROS DE ENTRADA E SAIDA                                 *
**********************************************************************
ABREFICH OPEN  (CMDIN,,CMDLOG,(OUTPUT))
         BR    R6
**********************************************************************
* ESPECIFICA AS CARACTERISTICAS DA MCS CONSOLE                       *
**********************************************************************
ESPPARAM LA    R10,OPERPRM         CARREGA END. OPERPRM EM R10
         USING MCSOPPRM,R10        ESTABELECE ENDERECAMENTO
         XC    OPERPRM(MCSOPLEN),OPERPRM  LIMPA AREA DE PARAM.
         MVI   MCSOMLV1,X'EC'      MESSAGE LEVEL
         MVI   MCSOAUTH,MCSOMSTR   CONSOLE MASTER
         MVI   MCSORCFL,MCSORCAL   TODOS OS ROUTE CODES
         MVI   MCSOLOGC,MCSOLOGN   HARDCOPY LOG NAO
         MVI   MCSOMIG,MCSOMIGY    OBTEM O MIGRATION ID
         BR    R6                  VOLTA PARA PRINCIPAL
**********************************************************************
* ATIVA MCS CONSOLE                                                  *
**********************************************************************
ATIVAMCS MODESET MODE=SUP,KEY=ZERO
         MCSOPER REQUEST=ACTIVATE,                                     X
               NAME=CNAME,                                             X
               TERMNAME=TNAME,                                         X
               OPERPARM=OPERPRM,                                       X
               MSGDLVRY=SEARCH,                                        X
               MSGECB=MECB,                                            X
               MCSCSA=(R3),                                            X
               MCSCSAA=(R3),                                           X
               CONSID=CNID,                                            X
               MIGIDREL=AUTO
         LTR   R15,R15             TESTA SE RC <> 0
         BNZ   FIMPROGE            SE RC <> 0 VAI PARA FIMPROGE
         MODESET MODE=PROB,KEY=NZERO
         XC    MECB,MECB           LIMPA ECB
         BR    R6
**********************************************************************
* LE COMANDO DE CMDIN                                                *
**********************************************************************
LECOMAND GET   CMDIN,TEXTAREA
         AP    CARTPACK(6),=P'1'
         UNPK  CARTBCD(5),CARTPACK(6)
         OI    CARTBCD+4,X'F0'
*
         TM    WTORFLG,X'01'
         BO    TRATAREP
         B     LECOMFIM
TRATAREP DS    0H
         CLC   TEXTAREA(2),=C'R '
         BE    EDITARE1
         CLC   TEXTAREA(6),=C'REPLY '
         BE    EDITARE2
         WTO   'COMANDO INVALIDO PARA WTOR',ROUTCDE=11
         B     FIMPROGE
EDITARE1 DS    0H
         MVC   TEXTAREA+2(2),REPLYID
         B     LECOMFIM
EDITARE2 DS    0H
         MVC   TEXTAREA+6(2),REPLYID
         B     LECOMFIM
LECOMFIM DS    0H
         SAC   0
         BR    R6
**********************************************************************
* EMITE COMANDO                                                      *
**********************************************************************
EMITECOM MODESET MODE=SUP,KEY=ZERO
MGCR     MGCRE TEXT=TEXTADDR,                                          X
               CONSID=CNID,                                            X
               CMDFLAG=NOHCPY,                                         X
               CART=UCART,                                             X
               MF=(E,MGCREC)
         MODESET MODE=PROB,KEY=NZERO
         MVC   AREAOUT(L'COMAND),COMAND
         MVC   AREAOUT+L'COMAND(L'TEXTAREA),TEXTAREA
         PUT   CMDLOG,AREAOUT
         BAS   R7,LIMPTEXT
         MVC   AREAOUT(18),CARTCHAR
         PUT   CMDLOG,AREAOUT
         BAS   R7,LIMPTEXT
         PUT   CMDLOG,AREAOUT
         BAS   R7,LIMPTEXT
         XC    WTORFLG(1),WTORFLG
         XC    REPLYID(2),REPLYID
         XC    MECB(L'MECB),MECB    LIMPA ECB
         BR    R6
**********************************************************************
* ENTRA EM ESTADO DE WAIT                                            *
**********************************************************************
AGUARESP EQU   *
         WAIT  ECB=MECB
         XC    MECB(L'MECB),MECB    LIMPA ECB
         BR    R6
**********************************************************************
* CAPTURA RESPOSTA AO COMANDO                                        *
**********************************************************************
CAPTRESP MODESET MODE=SUP,KEY=ZERO
         SAC   512                 ENTRA EM AR MODE
         SYSSTATE ASCENV=AR        ALERTA AS MACROS QUE ESTA EM AR
         MCSOPMSG REQUEST=GETMSG,                                      X
               CONSID=CNID,                                            X
               CMDRESP=YES,                                            X
               CART=UCART,                                             X
               RTNCODE=RC,                                             X
               RSNCODE=RSN
         LTR   R15,R15             TESTA SE RC <> 0
         BZ    CAPTCONT
         SAC   0
*        WTO   'NAO ACHOU RESPOSTA',ROUTCDE=11
         STIMER WAIT,DINTVL=TEMPO      AGUARDA 1 SEGUNDO
         B     CAPTRESP
CAPTCONT DS    0H
         LAE   R8,0(0,R1)          CARREGA AR1 EM AR8 E R1 EM R8
         USING MDB,R8              ESTAB. END. PARA MDB EM R8
         SAC   0                   VOLTA PARA PRIMARY MODE
         SYSSTATE ASCENV=P         ALERTA AS MACROS QUE ESTA EM PRIM.
         MODESET MODE=PROB,KEY=NZERO
         BR    R6
**********************************************************************
* POE RESPOSTA NO CMDLOG                                             *
**********************************************************************
LOGACMDS DS    0H
         BAS   R7,VERPROXM
*
         SAC   512                 ENTRA EM AR MODE
         SYSSTATE ASCENV=AR        ALERTA AS MACROS QUE ESTA EM AR
         CLC   MDBMID(4),=C'MDB '  EH UM MDB ??
         BE    MDBOK               SE SIM VAI PRA MDBOK
         SAC   0
         WTO   'NAO ACHOU MDB',ROUTCDE=11
         B     FIMPROGE            SE NAO VAI PRA ERRO
MDBOK    DS    0H
*        SAC   0
*        WTO   'ACHOU MDB',ROUTCDE=11
         SAC   512
         LR    R5,R8               COPIA INICIO DE MDB P/ R5
         AH    R5,MDBLEN           ACHA FIM DE MDB EM R5
         LR    R4,R8               COPIA INICIO DE MDB P/ R4
         LA    R8,MDBHLEN(R8)      ACHA INICIO DE MDBG
         DROP  R8
*
         USING MDBG,R8             ESTAB. ENDERECAM. PARA MDBG
         LH    R3,MDBGTYPE         CARREGA TIPO MDBG EM R3
         C     R3,=A(MDBGOBJ)
         BE    MDBGOK
         SAC   0
         WTO   'NAO ACHOU MDBG',ROUTCDE=11
         B     FIMPROGE
MDBGOK   DS    0H
*        SAC   0
*        WTO   'ACHOU MDBG',ROUTCDE=11
         SAC   512
         AH    R8,MDBGLEN
         DROP  R8
*
         USING MDBSCP,R8
         LH    R3,MDBCTYPE
         C     R3,=A(MDBCOBJ)
         BE    MDBCOK
*        SAC   0
*        WTO   'NAO ACHOU MDBC',ROUTCDE=11
         SAC   512
         B     MDBTENT
MDBCOK   DS    0H
*        SAC   0
*        WTO   'ACHOU MDBC',ROUTCDE=11
         SAC   512
         TM    MDBMLVL1,MDBMLR     VERIFICA SE EH WTOR
         BO    MDBWTOR             SE FOR MARCA FLAG WTORFLG
         B     MDBWTORN
MDBWTOR  DS    0H
         MVC   REPLYID(2),MDBCRPYI
         MVI   WTORFLG,X'01'
MDBWTORN DS    0H
         AH    R8,MDBCLEN
MDBTENT  DS    0H
         DROP  R8
*
         USING MDBT,R8
         LH    R3,MDBTTYPE
         C     R3,=A(MDBTOBJ)
         BE    MDBTOK
         SAC   0
         WTO   'NAO ACHOU MDBT',ROUTCDE=11
         CR    R5,R8               VERIFICA SE EH O ULTIMO
         BE    FIMMDB              SE SIM VAI PARA FIMMDB
         B     FIMPROGE
MDBTOK   DS    0H
*        SAC   0
*        WTO   'ACHOU MDBT',ROUTCDE=11
         SAC   512                 ENTRA EM AR MODE
         LH    R9,MDBTLEN          CARREGA TAMANHO DE MDBT EM R9
         LA    R2,MDBTMBOB         CARREGA TAMANHO DO HEADER EM R2
         SR    R9,R2               DIMINUI O TAMANHO DO HEADER
         BCTR  R9,0                SUBTRAI 1 DE R9 PARA EXEC
         EX    R9,EXECTEXT         MOVE TEXTO PARA AREA DE SAIDA
         SAC   0                   ENTRA EM PRIMARY MODE
         PUT   CMDLOG,AREAOUT      ENVIA TEXTO PARA CMDLOG
         BAS   R7,LIMPTEXT         LIMPA AREA DE SAIDA
         SAC   512                 ENTRA EM AR MODE
         AH    R8,MDBTLEN          CALCULA END. DO PROXIMO MDBT
         CR    R5,R8               VERIFICA SE EH O ULTIMO
         BE    FIMMDB              SE SIM VAI PARA FIMMDB
         B     MDBTENT             SE NAO VAI LER O PROX. MDBT
*
FIMMDB   DS    0H
         CLC   PROXMDB(4),=F'0'
         BE    NAOHAMDB
         L     R8,PROXMDB
         MVC   PROXMDB(4),=F'0'
         B     LOGACMDS
NAOHAMDB DS    0H
         SAC   0
         PUT   CMDLOG,AREAOUT      ENVIA UMA LINHA EM BRANCO
         BR    R6
EXECTEXT MVC   AREAOUT(0),MDBTMSGT
LIMPTEXT DS    0H
         MVI   AREAOUT,C' '
         MVC   AREAOUT+1(L'AREAOUT-1),AREAOUT
         BR    R7
**********************************************************************
* VERIFICA SE HA MAIS UM MDB                                         *
**********************************************************************
VERPROXM DS    0H
         LA    R11,MDBPLNNO
         SR    R8,R11              LOCALIZA MDBPREFIX
         SAC   512
         USING MDBPRFX,R8
         CLC   MDBPID(4),=C'MDBP'  EH UM MDBP ?'
         BE    MDBPOK              SE SIM VAI PRA MDBPOK
         SAC   0
         WTO   'NAO ACHOU MDBP',ROUTCDE=11
         B     FIMPROGE            SE NAO VAI PRA ERRO
MDBPOK   DS    0H
*        SAC   0
*        WTO   'ACHOU MDBP',ROUTCDE=11
         SAC   512
         CLC   MDBPNEXT(4),=F'0'
         BE    SOHUMMDB
         MVC   PROXMDB(4),MDBPNEXT
*        SAC   0
*        WTO   'HA MAIS MDB',ROUTCDE=11
SOHUMMDB DS    0H
         SAC   0
         AR    R8,R11              VOLTA AO MDB
         DROP  R8
         BR    R7
**********************************************************************
* DESATIVA MCS CONSOLE                                               *
**********************************************************************
DESATMCS MODESET MODE=SUP,KEY=ZERO
         MCSOPER REQUEST=DEACTIVATE,                                   X
               CONSID=CNID
         LTR   R15,R15             TESTA SE RC <> 0
         BNZ   FIMPROGE            SE RC <> 0 VAI PARA FIMPROGE
         MODESET MODE=PROB,KEY=NZERO
         BR    R6
**********************************************************************
* FECHA FICHEIROS DE ENTRADA E SAIDA                                 *
**********************************************************************
FECHFICH CLOSE (CMDIN,,CMDLOG)
         BR    R6
**********************************************************************
* FIM DO PROGRAMA COM RC=0                                           *
**********************************************************************
FIMPROG  SAC   0                   VOLTA PARA PRIMARY MODE
         SYSSTATE ASCENV=P         ALERTA AS MACROS QUE ESTA EM PRIM.
         XR    R15,R15
         PR
**********************************************************************
* FIM DO PROGRAMA COM ERRO                                           *
**********************************************************************
FIMPROGE SAC   0                   VOLTA PARA PRIMARY MODE
         SYSSTATE ASCENV=P         ALERTA AS MACROS QUE ESTA EM PRIM.
         ABEND 1000,DUMP
         LA    R15,8
         PR
**********************************************************************
* DCB'S  DOS FICHEIROS DE ENTRADA E SAIDA                            *
**********************************************************************
CMDIN    DCB   DDNAME=CMDIN,DSORG=PS,MACRF=GM,LRECL=80,RECFM=FB,       X
               EODAD=DESMCSCO
CMDLOG   DCB   DDNAME=CMDLOG,DSORG=PS,MACRF=PM,LRECL=132,RECFM=FB
**********************************************************************
* DEFINICOES DE VARIAVEIS                                            *
**********************************************************************
CNAME    DC    CL8'BSYSA   '       NOME DA CONSOLE
TNAME    DC    CL8'BATCH'          NOME FICTICIO DO TERMINAL
*
COMAND   DC    C' COMANDO: '
CARTCHAR DC    C' CART...: '
UCART    DC    CL3'MCM'            COMMAND AND RESPONSE TOKEN
CARTBCD  DS    CL5
*
CNID     DS    F                   CONSOLE ID
MECB     DS    F                   ECB DA MCS CONSOLE
TEXTADDR DC    A(TEXTALEN)         ENDERECO DA AREA DE COMANDOS
TEXTALEN DC    AL2(L'TEXTAREA)     TAMANHO DA AREA DE COMANDOS
TEXTAREA DS    CL80                AREA DE COMANDOS
AREAOUT  DS    CL132               AREA DE CMDLOG
OPERPRM  DS    CL(MCSOPLEN)        AREA DE PARAMETROS DA MCS
RC       DS    F
RSN      DS    F
REPLYID  DS    CL2
MCART    DS    CL8
WTORFLG  DC    XL1'00'
PROXMDB  DC    F'0'
CARTPACK DC    PL6'0'              CONTADOR PARA CART
         DS    0D
TEMPO    DC    C'00000100'
MGCREC   MGCRE MF=L
**********************************************************************
* DSECTS                                                             *
**********************************************************************
         IEAVG132 ,                MDB PREFIX
         IEAVM105 ,                MDB
         IEAVG131 ,                CONSOLE STATUS AREA
         IEZVG111 ,                OPERPARM PARAMETER AREA
**********************************************************************
* LISTA DE EQUATES                                                   *
**********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
