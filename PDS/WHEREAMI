***********************************************************************
* PROGRAM: WHEREAMI                                                   *
*                                                                     *
* AUTHOR: PHILIPPE LEITE                                              *
*                                                                     *
* FUNCTION: RETURN TO THE CALLER A RC INDICATING THE EXECUTION        *
*           ENVIRONMENT.                                              *
*           RC = 1  -> BATCH                                          *
*           RC = 2  -> ONLINE (CICS)                                  *
*           RC = 3  -> CICS BATCH ???????? :P                         *
*           RC = 4  -> TSO                                            *
*           RC = 5  -> TSO BATCH                                      *
*                                                                     *
* OBS.: THIS PROGRAM CAN LINKEDITED AS REENTRANT                      *
*                                                                     *
***********************************************************************
WHEREAMI CSECT
WHEREAMI AMODE 31
WHEREAMI RMODE ANY
         BAKR  R14,0
         USING WHEREAMI,R12
         LR    R12,R15
         LA    R10,0
         USING PSA,R10
         L     R10,PSAAOLD
         DROP  R10
         USING ASCB,R10
         L     R10,ASCBASXB
         DROP  R10
         USING ASXB,R10
         L     R2,ASXBFTCB
         USING TCB,R2
         USING RBBASIC,R3
         USING CDENTRY,R4
         XR    R8,R8
         XR    R9,R9
LOOPTCBQ EQU   *
         L     R3,TCBRBP
         ICM   R4,B'0111',RBCDE1
         CLC   CDNAME,=C'IEAVAR00'
         BE    NEXTTCB
         CLC   CDNAME,=C'IEAVTDST'
         BE    NEXTTCB
         CLC   CDNAME,=C'IEFSD060'
         BE    NEXTTCB
*
         CLC   CDNAME,=CL8'IEFIIC'
         BNE   CONTIIC
         WTO   'BATCH INITIATOR',ROUTCDE=11
         IC    R8,=X'01'
         OR    R9,R8
         B     NEXTTCB
CONTIIC  EQU   *
         CLC   CDNAME,=CL8'DFHSIP'
         BNE   CONTSIP
         WTO   'CICS',ROUTCDE=11
         IC    R8,=X'02'
         OR    R9,R8
         B     NEXTTCB
CONTSIP  EQU   *
         CLC   CDNAME,=C'IKJEFT01'
         BNE   CONTIKJ
         WTO   'TSO',ROUTCDE=11
         IC    R8,=X'04'
         OR    R9,R8
CONTIKJ  EQU   *
NEXTTCB  EQU   *
         L     R2,TCBTCB
         LTR   R2,R2
         BZ    ENDLOOP
         B     LOOPTCBQ
ENDLOOP  EQU   *
         LR    R15,R9
         PR
         LTORG
         IHAPSA
         IHAASCB
         IHAASXB
         IKJTCB
         IHARB
         IHACDE
         YREGS
         END   WHEREAMI
