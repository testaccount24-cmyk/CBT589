HSC@UNSC CSECT
HSC@UNSC AMODE 31
HSC@UNSC RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING HSC@UNSC,R12
***********************************************************************
* UNSCRATCH FUNCTION IMPLEMENTATION                                   *
***********************************************************************
         LR    R11,R1
         USING WORKAREA,R11
*
         L     R10,ARGTADDR
         USING ARGTABLE_ENTRY,R10
         LA    R10,8(R10)
         CLC   ARGTABLE_ARGSTRING_PTR(8),=XL8'FFFFFFFFFFFFFFFF'
         BE    ERROPARM
         L     R5,ARGTABLE_ARGSTRING_PTR
         L     R4,ARGTABLE_ARGSTRING_LENGTH
         CL    R4,=F'6'                IF VOLUME > 6 GOTO ERROPARM
         BH    ERROPARM
         BCTR  R4,0
         EX    R4,MOVEVOLM
*
         SLSXREQ EJECT,                                                X
               OPTION=SYNCH,                                           X
               VOLSER=VOLUME,                                          X
               XCALADR=WXCALADR,                                       X
               MF=(E,SLSXPARM)
         LTR   R15,R15
         BNZ   HSCNOACT
         LR    R7,R1
         USING SLX,R7
         XR    R15,R15
         ICM   R15,B'0001',SLXCMDRC
         LA    R3,24
         CR    R15,R3                  RC > 24 ?
         BH    ERROINVL                YES, INVALID RC
         B     *+4(R15)
         B     CMDOK                   RC = 00 ?
         B     ERROUSCR                RC = 04 ?
         B     ERROPARM                RC = 08 ?
         B     ERROUNRC                RC = 12 ?
         B     ERROUNSC                RC = 16 ?
         B     HSCNOACT                RC = 20 ?
         B     ERRONAUT                RC = 24 ?
CMDOK    EQU   *
         BAS   R6,SETVAR               GO SET VARIABLES
         PR
ERROUSCR EQU   *
         LA    R15,4
         PR
ERROPARM EQU   *                       INVALID PARM SPECIFIED
         LA    R15,8
         PR
ERROUNRC EQU   *                       UNRECOVERABLE ERROR
         LA    R15,12
         PR
ERROUNSC EQU   *                       UNSUCCESSFUL REQUEST
         BAS   R6,SETVAR               GO SET VARIABLES
         LA    R15,16
         PR
HSCNOACT EQU   *                       HSC NOT AVAILABLE
         LA    R15,20
         PR
ERRONAUT EQU   *                       FUNCTION NOT AUTHORIZED
         LA    R15,24
         PR
ERROINVL EQU   *                       INVALID RETURN CODE
         LA    R15,50
         PR
ERROSETV EQU   *                       SETVAR ROUTINE ERROR
         LA    R15,60
         PR
***********************************************************************
* SET VARIABLES SUBROUTINE                                            *
***********************************************************************
SETVAR   EQU   *
         MVC   NAMEVAR(9),=C'@SLXCMDRC'  RETURN CODE VARIABLE
         MVC   SHVNAML(4),=F'9'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   HEXNUMBR(1),SLXCMDRC
         UNPK  BCDNUMBR(3),HEXNUMBR(2)
         NC    BCDNUMBR,=X'0F0F'
         TR    BCDNUMBR(2),HEXTABLE
         MVC   VALUEVAR(2),BCDNUMBR
         MVC   SHVVALL(4),=F'2'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
*
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
*
         MVC   NAMEVAR(7),=C'@SLXSRC'  REASON CODE VARIABLE
         MVC   SHVNAML(4),=F'7'
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         MVC   HEXNUMBR(4),SLXSRC
         UNPK  BCDNUMBR(9),HEXNUMBR(5)
         NC    BCDNUMBR,=X'0F0F0F0F0F0F0F0F'
         TR    BCDNUMBR(8),HEXTABLE
         MVC   VALUEVAR(8),BCDNUMBR
         MVC   SHVVALL(4),=F'8'
         LA    R15,VALUEVAR
         ST    R15,SHVVALA
*
         LR    R1,R11
         L     R15,=V(HSC@SETV)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERROSETV
         BR    R6
*
MOVEVOLM MVC   VOLUME(0),0(R5)
***********************************************************************
* CONSTANTS                                                           *
***********************************************************************
HEXTABLE DC    C'0123456789ABCDEF'
         LTORG
*
         HSCWAREA
         SLSXREQM
         IRXEFPL
         IRXARGTB
         IRXEVALB
         YREGS
         END   HSC@UNSC
