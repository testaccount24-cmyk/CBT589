DEV2VOL  CSECT
DEV2VOL  AMODE 31
DEV2VOL  RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING DEV2VOL,R12
         MODID ,
**********************************************************************
* PROGRAM: DEV2VOL                                                   *
*                                                                    *
* AUTHOR: PHILIPPE LEITE                                             *
*                                                                    *
* OBJECTIVE: REXX EXTERNAL FUNCTION TO CONVERT DEVICE NUMBER TO      *
*            VOLUME SERIAL.                                          *
*                                                                    *
* VARIABLES RETURNED:                                                *
*                                                                    *
*   @DEV2VOL - VOLUME SERIAL                                         *
*                                                                    *
*  POSSIBLE RETURN CODES:                                            *
*                                                                    *
*   00 - SUCCESSFUL EXECUTION                                        *
*   04 - UCB NOT FOUND                                               *
*   08 - DEVICE IS OFFLINE                                           *
*   16 - INVALID PARAMETER                                           *
*                                                                    *
* EXAMPLE:                                                           *
*                                                                    *
*      /* REXX */                                                    *
*      RC = DEV2VOL('E774')                                          *
*      IF RC = 0 THEN SAY @DEV2VOL                                   *
*                                                                    *
* CREATION DATE: 27/05/2004                                          *
*                                                                    *
**********************************************************************
         LR    R7,R0              SAVE ENV BLOCK ADDRESS
         LR    R9,R1              SAVE EFPL ADDRESS
         USING EFPL,R9
*
         STORAGE OBTAIN,LENGTH=WAREALEN
         LR    R8,R1
         USING WKAREA,R8
*
         USING ENVBLOCK,R7          SET ADDRESSABILITY TO ENVBLOCK
         L     R6,ENVBLOCK_IRXEXTE  EXTERNAL ENTRY VECTOR ADDRESS
         USING IRXEXTE,R6           SET ADDRESSABILITY TO EXTE
         L     R0,IRXEXCOM          IRXEXCOM ENTRY ADDRESS
         ST    R0,IRXEXCO@
         DROP  R6
         DROP  R7
*
         L     R10,EFPLARG
         USING ARGTABLE_ENTRY,R10
         L     R5,ARGTABLE_ARGSTRING_PTR
         L     R4,ARGTABLE_ARGSTRING_LENGTH
         CL    R4,=F'4'           PARAMETER LENGTH = 4 ?
         JE    CONTMOVD
         MVC   RETRCODE,=F'16'    NO, SET RC = 16
         J     CONVRETR
CONTMOVD EQU   *
         MVC   DEVICE,0(R5)
         TR    DEVICE(4),CHRTABLE
         PACK  DEVICE(4),DEVICE(4)
         L     R5,DEVICE
         SRL   R5,4
         STH   R5,DEV
*
SCANLOOP EQU   *
         UCBSCAN COPY,                                                 X
               DEVN=DEV,                                               X
               UCBAREA=UCBCOPY,                                        X
               WORKAREA=SCANWORK,                                      X
               DYNAMIC=YES,                                            X
               NONBASE=NO,                                             X
               RANGE=ALL,                                              X
               DEVCLASS=DASD,                                          X
               MF=(E,UCBSCANL)
         ST    R15,RETRCODE
         LA    R11,UCBCOPY
         USING UCBCMSEG,R11
         CLC   DEV,UCBCHAN        DEVICE EXISTS ?
         JE    CONTCHAN
         LA    R0,4               NO, SET RC = 4
         ST    R0,RETRCODE
         J     CONVRETR
CONTCHAN EQU   *
         TM    UCBSTAT,UCBONLI    DEVICE IS ONLINE ?
         JO    CONTSCAN
         LA    R0,8               NO, SET RC = 8
         ST    R0,RETRCODE
         J     CONVRETR
CONTSCAN EQU   *
         MVC   VOLUME,UCBVOLI     MOVE VOLSER
*
CONVRETR EQU   *
         L     R11,EFPLEVAL
         L     R11,0(R11)
         USING EVALBLOCK,R11
         L     R5,RETRCODE
         CVD   R5,PACKCODE
         UNPK  EBCDCODE,PACKCODE
         OC    EBCDCODE,=2X'F0'
         MVC   EVALBLOCK_EVLEN,=F'2'
         MVC   EVALBLOCK_EVDATA(2),EBCDCODE
         LTR   R5,R5
         BNZ   ENDD2V
*
CONTSETV EQU   *
         LA    R7,PARAM4
         USING SHVBLOCK,R7
         MVC   NAMEVAR,=C'@DEV2VOL'    SET VARIABLE NAME
         MVC   SHVNAML,=F'8'           SET VARIABLE NAME LENGTH
         MVC   SHVVALL,=F'6'           SET VARIABLE LENGTH
         LA    R15,NAMEVAR
         ST    R15,SHVNAMA
         LA    R15,VOLUME
         ST    R15,SHVVALA
         BAS   R6,DEV@SETV
*
ENDD2V   EQU   *
         STORAGE RELEASE,LENGTH=WAREALEN,ADDR=(R8)
         XR    R15,R15
         PR
DEV@SETV EQU   *
         MVC   PARAM1,=C'IRXEXCOM'
         MVI   SHVCODE,C'S'
         L     R15,IRXEXCO@
         CALL  (15),(PARAM1,0,0,PARAM4),VL,MF=(E,EXCOPLST)
         BR    R6
*
         YREGS
         LTORG
*---------------------------------------------------------------------*
*  WORK AREA                                                          *
*---------------------------------------------------------------------*
CHRTABLE DC    X'00000000000000000000000000000000'   00-0F
         DC    X'00000000000000000000000000000000'   10-1F
         DC    X'00000000000000000000000000000000'   20-2F
         DC    X'00000000000000000000000000000000'   30-3F
         DC    X'00000000000000000000000000000000'   40-4F
         DC    X'00000000000000000000000000000000'   50-5F
         DC    X'00000000000000000000000000000000'   60-6F
         DC    X'00000000000000000000000000000000'   70-7F
         DC    X'000A0B0C0D0E0F000000000000000000'   80-8F
         DC    X'00000000000000000000000000000000'   90-9F
         DC    X'00000000000000000000000000000000'   A0-AF
         DC    X'00000000000000000000000000000000'   B0-BF
         DC    X'000A0B0C0D0E0F000000000000000000'   C0-CF
         DC    X'00000000000000000000000000000000'   D0-DF
         DC    X'00000000000000000000000000000000'   E0-EF
         DC    X'00010203040506070809000000000000'   F0-FF
*---------------------------------------------------------------------*
*  DSECT WORK AREA                                                    *
*---------------------------------------------------------------------*
WKAREA   DSECT
PACKCODE DS    PL8
EBCDCODE DS    CL2
RETRCODE DS    F
IRXEXCO@ DS    F
EXCOPLST DS    4F
NAMEVAR  DS    CL8
DEVICE   DS    CL4
DEV      DS    H
VOLUME   DS    CL6
TOKEN    DS    CL48
UCBCOPY  DS    CL48
SCANWORK DS    CL100
* IRXEXCOM PARAMETER AREA
PARAM1   DS    CL8
PARAM2   DS    A
PARAM3   DS    A
PARAM4   DS    XL32
         UCBSCAN MF=(L,UCBSCANL)
WAREALEN EQU   *-WKAREA
*---------------------------------------------------------------------*
*  DSECTS AREA                                                        *
*---------------------------------------------------------------------*
         IRXEFPL
         IRXENVB
         IRXEXTE
         IRXSHVB
         IRXARGTB
         IRXEVALB
         IEFUCBOB DEVCLAS=DA
         END   DEV2VOL
