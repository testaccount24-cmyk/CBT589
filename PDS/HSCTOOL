HSCTOOL  CSECT
HSCTOOL  AMODE 31
HSCTOOL  RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING HSCTOOL,R12
         B     HEADER
         DC    C'HSCTOOL - VERSION 1.1.4 '
         DC    C'&SYSDATE '
         DC    C'&SYSTIME '
HEADER   DS    0H
***********************************************************************
* PROGRAM: HSCTOOL                                                    *
*                                                                     *
* AUTHOR: PHILIPPE LEITE                                              *
*                                                                     *
* OBJECTIVE: HSC (STORAGETEK) - REXX EXTERNAL FUNCTION                *
*                                                                     *
* CREATION DATE: 29/07/2002                                           *
*                                                                     *
***********************************************************************
         LR    R7,R0                   Save environment address
         LR    R9,R1
         USING EFPL,R9
*
         STORAGE OBTAIN,LENGTH=WAREALEN
         LR    R11,R1
         USING WORKAREA,R11
         LR    R2,R11             get addr of work area
         LA    R3,WAREALEN        let length of work area
         SR    R15,R15            pad of X'00' & Zero length
         MVCL  R2,R14             Init workarea to zeros
*
         L     R10,EFPLARG
         ST    R10,ARGTADDR
         USING ARGTABLE_ENTRY,R10
         L     R5,ARGTABLE_ARGSTRING_PTR
         L     R4,ARGTABLE_ARGSTRING_LENGTH
         CL    R4,=F'9'                IF PARM > 9 GOTO ERROPARM
         BH    ERROPARM
         BCTR  R4,0
         EX    R4,MOVEFUNC
*
         USING ENVBLOCK,R7          set addressability to envblock
         ST    R7,ENVB@
         L     R7,ENVBLOCK_IRXEXTE  external entry vector address
         USING IRXEXTE,R7           set addressability to exte
         L     R0,IRXEXCOM          IRXEXCOM entry address
         ST    R0,IRXEXCO@
         DROP  R7
*
         LOAD  EP=SLSXCAL
         ST    R0,WXCALADR
         MVC   EXITCODE(2),=C'00'
*
         CLC   FUNCTION(4),=C'QHSC'
         BE    SET@QHSC
         CLC   FUNCTION(4),=C'QDSN'
         BE    SET@QDSN
         CLC   FUNCTION(7),=C'QDRIVES'
         BE    SET@QDRV
         CLC   FUNCTION(8),=C'QSCRATCH'
         BE    SET@QSCR
         CLC   FUNCTION(5),=C'MOUNT'
         BE    SET@MONT
         CLC   FUNCTION(8),=C'DISMOUNT'
         BE    SET@DISM
         CLC   FUNCTION(5),=C'EJECT'
         BE    SET@EJCT
         CLC   FUNCTION(7),=C'SCRATCH'
         BE    SET@SCRT
         CLC   FUNCTION(9),=C'UNSCRATCH'
         BE    SET@UNSC
         CLC   FUNCTION(4),=C'MOVE'
         BE    SET@MOVE
         CLC   FUNCTION(7),=C'QVOLUME'
         BE    SET@QVOL
         CLC   FUNCTION(4),=C'QCAP'
         BE    SET@QCAP
         CLC   FUNCTION(7),=C'QCONFIG'
         BE    SET@QCON
         B     ERROPARM
SET@QHSC EQU   *
         LR    R1,R11
         L     R15,=V(HSC@QHSC)
         BASR  R14,R15
         B     ENDHSC
SET@QDSN EQU   *
         LR    R1,R11
         L     R15,=V(HSC@QDSN)
         BASR  R14,R15
         B     ENDHSC
SET@QDRV EQU   *
         LR    R1,R11
         L     R15,=V(HSC@QDRV)
         BASR  R14,R15
         B     ENDHSC
SET@QSCR EQU   *
         LR    R1,R11
         L     R15,=V(HSC@QSCR)
         BASR  R14,R15
         B     ENDHSC
SET@MONT EQU   *
         LR    R1,R11
         L     R15,=V(HSC@MONT)
         BASR  R14,R15
         B     ENDHSC
SET@DISM EQU   *
         LR    R1,R11
         L     R15,=V(HSC@DISM)
         BASR  R14,R15
         B     ENDHSC
SET@EJCT EQU   *
         LR    R1,R11
         L     R15,=V(HSC@EJCT)
         BASR  R14,R15
         B     ENDHSC
SET@SCRT EQU   *
         LR    R1,R11
         L     R15,=V(HSC@SCRT)
         BASR  R14,R15
         B     ENDHSC
SET@UNSC EQU   *
         LR    R1,R11
         L     R15,=V(HSC@UNSC)
         BASR  R14,R15
         B     ENDHSC
SET@MOVE EQU   *
         LR    R1,R11
         L     R15,=V(HSC@MOVE)
         BASR  R14,R15
         B     ENDHSC
SET@QVOL EQU   *
         LR    R1,R11
         L     R15,=V(HSC@QVOL)
         BASR  R14,R15
         B     ENDHSC
SET@QCAP EQU   *
         LR    R1,R11
         L     R15,=V(HSC@QCAP)
         BASR  R14,R15
         B     ENDHSC
SET@QCON EQU   *
         LR    R1,R11
         L     R15,=V(HSC@QCON)
         BASR  R14,R15
ENDHSC   EQU   *
         CVD   R15,DECNUMBR
         UNPK  EXITCODE(2),DECNUMBR(8)
         OC    EXITCODE(2),=X'F0F0'
         L     R2,EFPLEVAL
         L     R2,0(R2)
         USING EVALBLOCK,R2
         MVC   EVALBLOCK_EVLEN(4),=F'2'
         MVC   EVALBLOCK_EVDATA(2),EXITCODE
*
         STORAGE RELEASE,LENGTH=WAREALEN,ADDR=(R11)
         SLR   R15,R15
         PR
ERROPARM EQU   *
         LA    R15,8
         B     ENDHSC
***********************************************************************
*  MVC USED IN EX INSTRUCTIONS                                        *
***********************************************************************
MOVEFUNC MVC   FUNCTION(0),0(R5)
         LTORG
***********************************************************************
*  DSECTS AREA                                                        *
***********************************************************************
         HSCWAREA
         SLSXREQM
         IRXEFPL
         IRXENVB
         IRXEXTE
         IRXARGTB
         IRXEVALB
         IKJTSVT
***********************************************************************
*  EQUATE AREA                                                        *
***********************************************************************
         YREGS
         END   HSCTOOL
